```
; 本题给出的形式
(lambda <vars>
  (let ((u '*unassigned*)
        (v '*unassigned*))
    (let ((a <e1>)
          (b <e2>))
      (set! u a)
      (set! v b))
    <e3>))

(define (solve f y0 dt)
  (define y (integral (delay dy) y0 dt))
  (define dy (stream-map f y)))

; 正文给出的形式
(lambda <vars>
  (let ((u '*unassigned*)
        (v '*unassigned*))
    (set! u <e1>)
    (set! v <e2>)
    <e3>))
```

正文中还有一句提示：

> 本题给出的形式，它将强加一种限制，要求被定义的变量的值能在不用其他变量的值的情况下进行求值。

也就是说，本题给出的形式比正文的更加严格，而且根据作者的意图，在求解`solve`时，正文中的展开形式是可行大，而本题中的形式是不行的。

我们可以将`solve`代入本题给出的形式中
```
(define (solve f y0 dt)
  (let ((y '*unassigned*)
        (dy '*unassigned*))
    (let ((a (integral (delay dy) y0 dt))
          (b (stream-map f y)))
      (set! y a)
      (set! dy b))
    y))
```
再把内部的`let`展开
```
(define (solve f y0 dt)
  (let ((y '*unassigned*)
        (dy '*unassigned*))
    ((lambda (a b)
       (set! y a)
       (set dy b))
     (integral (delay dy) y0 dt)
     (stream-map f y))
    y))
```
在求值`(stream-map f y)`时，需要`y`的值，而这时`y`还是`'*unassigned*`，所以这时会出错；需要注意的是在求值`(integral (delay dy) y0 dt)`时并不会出错，因为其中的`dy`已经延时求值了。

而正文中给出的形式在求值`y`时，`y`已经定义好了，所以是没问题的
```
(define (solve f y0 dt)
  (let ((y '*unassigned*)
        (dy '*unassigned*))
    (set! y (integral (delay dy) y0 dt))
    (set! dy (stream-map f y))
    y))
```
