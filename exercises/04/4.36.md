假设用`an-integer-starting-from`替换`an-integer-between`的话，由于`an-integer-starting-from`能够产生出一个无穷序列，在产生第一个合适的毕达哥拉斯三元组后，`k`的值一直增加（这里假设采用书中介绍的`深度优先`的搜索算法），而这时永远不能满足毕达哥拉斯的条件，造成`假死`现象。

根据上面的分析，要想生成所有的毕达哥拉斯三元组，必须保障`i`、`j`、`k`三个变量中至少两个有上界，这样就不至于出现死循环了。

我们知道，`k`肯定是`i`、`j`、`k`这三者中最大的，如果`k`的值能够确定出来，那么`i`、`j`的上界也就自然确定了。有了这个思路我们就可以写出下面的一种解决方案：

```
(define (all-pythagorean-triple)
  (let ((k (an-integer-starting-from 1)))
    (let ((j (an-integer-between 1 k)))
      (let ((i (an-integer-between 1 j)))
        (require (= (+ (* i i) (* j j))
                    (* k k)))
        (list i j k)))))
```

https://wizardbook.wordpress.com/2011/01/12/exercise-4-36-2/ 这篇文章给出了另一种解决思路，大家可以参考。

由于原文被GFW墙了，所以这里把全文复制粘贴过来了：

Looking at how an-integer-starting-from is written shows that the following code wouldn’t work because there is no limit placed on the value k can take:
```
(let* ((i (an-integer-starting-from low))
       (j (an-integer-starting-from i))
       (k (an-integer-starting-from j)))
  (require (= (square k) (+ (square i) (square j)))))

```
This code would never end because the initial values i = j = k = low. The solution is k = √2 low which is not an integer. This will cause the require to fail, an-integer-starting-from will then backtrack to the next possible value of k, which of course will also fail and since there is no limit on k the procedure will never complete. Even if we set low to 3 and start j from i+1 the first solution of (3,4,5) will be found, but on calling try-again for another solution, the same situation occurs.

Looking at the next exercise gives a clue though. Since k can be calculated given i and j we could re-write the let expression:
```
(let* ((i (an-integer-starting-from low))
       (j (an-integer-starting-from (+ i 1)))
       (k (sqrt (+ (square i) (square j)))))
  (require (integer? k))
```

This is an improvement and it will work for some values, for example (an-integer-starting-from 3) returns '(3 4 5) but it will never generate a second value for the same reason above except this time there is no limit on j.

The trick is to constrain the values that j can take. Looking at the [wikipedia article on generating Pythagorean triples](https://en.wikipedia.org/wiki/Pythagorean_triple#The_Platonic_sequence) gives a solution.  
```
(define (a-pythagorean-triple-from low)
  (define (limit i) (if (odd? i)
                        (/ (- (square i) 1) 2)
                        (- (/ (square i) 4) 1)))
  (let* ((i (an-integer-starting-from low))
         (j (an-integer-between i (limit i)))
         (k (sqrt (+ (square i) (square j)))))
    (require (integer? k))
    (list i j k)))
```    
The wikipedia article states that this formula doesn’t produce all Pythagorean triples though, so I’m not sure if this is correct. I’m also not sure that the constraint on the maximum value of j is valid. Having said that, all of the values from [Exercise 3.69](https://wizardbook.wordpress.com/2010/12/22/exercise-3-69/) came up in this version and searching for more solutions is much faster than the streams version. In fact I can’t even find seven solutions using the stream version without my machine grinding to a halt. Obviously the more we search using the amb version the longer it will take to find the next solution, but it’s a superior approach for this problem.
