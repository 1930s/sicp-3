## 内容

- 1.33小节，过程作为一般性的方法，习题1.34、1.35

1. 通过区间折半寻找方程的根
2. 找出函数的不动点

## 笔记

- 所谓函数的不动点，就是x满足`f(x)=x`。对于某些函数，我们可以从一个初始值开始，不断应用函数`f`，直到值变化不大时，就能够找出一个不动点。
```
f(x), f(f(x)), f(f(f(x))), f(f(f(f(x)))), .....
```

```
(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
        next
        (try next))))
  (try first-guess))

(fixed-point cos 1.0)
;Value: .7390822985224023
```

实际上，我们可以把将之前求平方根的的计算转化为一个寻找不动点的过程：
> 计算某个数x的平方根，就是要找到一个y使得y<sup>2</sup>=x，这一等式可以变化为y=x/y，可以发现，我们就是要寻找不定点的函数为函数`f(y)=x/y`，这里的x为已知数。
```
(define (sqrt x)
  (fixed-point (lambda (y) (/ x y))
               1.0))
```

但是上面这个求不动点的过程，不一定收敛。有可能结果进入一个无限循环，比如：
> 初始猜测值为y<sub>1</sub>，下一个猜测是y<sub>2</sub>=x/y<sub>1</sub>，而再下一个猜测是y<sub>3</sub>=x/y<sub>2</sub>=x/(x/y<sub>1</sub>)=y<sub>1</sub>。
控制这类震荡的一种方式是让猜测值变化的别太剧烈，因为实际答案总是在两个猜测值y和x/y之间的，所以我们可以做出一个猜测，使之不像x/y那样远离y，为此可以用y和x/y的平均值。改进后的sqrt过程如下：
```
(define (sqrt x)
  (fixed-point (lambda (y) (average y (/ x y)))
               1.0))
```

其实，这里改进后求平方根的方式，和之前1.1.7节原来那个求平方根过程产生的序列是一样的。